# APP INSIGHTS VARIABLES

variable "principal_secret" {
  type        = string
  description = "Secret to be used in the Service Principal connection from Power Platform to the Azure AI Search resource"
}

variable "resource_share_user" {
  type        = string
  description = "The Entra ID of the interactive admin user who should have initial access to the resources generated by this pattern."
}

variable "app_insights_sections" {
  type = map(object({
    query = string
    name  = string
    chart = string
  }))
  default = {
    section_1 = {
      query = "traces\n| where message contains \"Log Number\"\n| summarize requestCount = count() by bin(timestamp, 5s)\n| order by timestamp asc"
      name  = "Requests over time."
      chart = "unstackedbar"
    }
    section_2 = {
      query = "traces\n| where message contains \"Log Number\"\n| extend topic=customDimensions[\"topic\"]\n| summarize amount=count() by bin(timestamp, 10s), tostring(topic)"
      name  = "All requests split into topics."
      chart = "barchart"
    }
    section_3 = {
      query = "traces\n| where message contains \"Log Number\"\n| extend topic=customDimensions[\"topic\"]\n| limit 20"
      name  = "Last 20 requests."
      chart = "table"
    }
    section_4 = {
      query = "traces\n| where message contains \"Log Number\"\n| extend app_timestamp_real = todouble(customDimensions[\"timestamp\"])\n| extend app_timestamp = unixtime_seconds_todatetime(app_timestamp_real)\n| extend time_difference = tolong(timestamp - app_timestamp)\n| summarize average=avg(time_difference) by bin(timestamp, 5s)"
      name  = "Response time by timestamp."
      chart = "timechart"
    }
    section_5 = {
      query = "traces\n| where message contains \"Log Number\"\n| extend app_timestamp_real = todouble(customDimensions[\"timestamp\"])\n| extend app_timestamp = unixtime_seconds_todatetime(app_timestamp_real)\n| extend time_difference = tolong(timestamp - app_timestamp)\n| summarize average=avg(time_difference)"
      name  = "Avg time per response overall."
      chart = "stat"
    }
    section_6 = {
      query = "traces\n| where message contains \"Log Number\"\n| extend topic=customDimensions[\"topic\"]\n| extend app_timestamp_real = todouble(customDimensions[\"timestamp\"])\n| extend app_timestamp = unixtime_seconds_todatetime(app_timestamp_real)\n| extend time_difference = tolong(timestamp - app_timestamp)\n| summarize average=avg(time_difference) by bin(timestamp, 10s), tostring(topic)\n| render barchart"
      name  = "Avg time per response per topic."
      chart = "barchart"
    }
  }
  description = "A map of App Insights sections, each containing a KQL query, name, and chart type."
}

variable "app_insights_workbook_description" {
  default     = "# Description of Workbook\n\nThis workbook is designed as a starting point to monitor your Copilot and template for further workbooks.\n\n## Queries\n\nThe default queries include:\n\n1. Number of incoming requests over time overall\n2. Number of Requests split into topics\n3. List of last n requests (default: n = 20)\n4. Response time by timestamp (scatter chart or timechart to see outliers)\n5. Avg time per response overall \n6. Avg time per response per topic"
  description = "The description at the top of the workbook, in markdown format"
}

variable "cognitive_deployments" {
  type = map(object({
    name = string
    model = object({
      format  = string
      name    = string
      version = string
    })
    scale = object({
      type = string
    })
    rai_policy_name = string
  }))
  default = {
    "gpt-4" = {
      name = "text-embedding-3-large"
      model = {
        format  = "OpenAI"
        name    = "text-embedding-3-large"
        version = "1"
      }
      scale = {
        type = "Standard"
      }
      rai_policy_name = "Microsoft.DefaultV2"
    }
  }
  description = <<DESCRIPTION
  A map of cognitive model deployments to create on the Azure OpenAI Cognitive Services account. The map key is deliberately arbitrary to avoid issues where map keys maybe unknown at plan time.
  - `name` - (Required) The name of the deployment.
  - `model` - (Required) The model to deploy.
    - `format` - "OpenAI"  
    - `name` - The name of the model to deploy.
    - `version` - The version of the model to deploy.
  - `scale` - (Required) The scale of the model.
    - `type` - The type of scale to use. Possible values are `Standard`.
  - `rai_policy_name` - (Required) The name of the RAI policy to use for the deployment.
  example:
  ```
  {
    "gpt-4" = {
      name = "gpt-4"
      model = {
        format  = "OpenAI"
        name    = "gpt-4"
        version = "0125-Preview"
      }
      scale = {
        type = "Standard"
      }
      rai_policy_name = "Microsoft.DefaultV2"
    }
    "text-embedding-ada-002" = {
      name = "text-embedding-ada-002"
      model = {
        format  = "OpenAI"
        name    = "text-embedding-ada-002"
        version = "2"
      }
      scale = {
        type = "Standard"
      }
      rai_policy_name = "Microsoft.DefaultV2"
    }
  }
  ```
  DESCRIPTION
}

variable "cps_container_name" {
  type        = string
  default     = "copilot-studio-sample-data"
  description = "The name of the storage container for the Copilot Studio bot's test data."
}

variable "cps_storage_replication_type" {
  type        = string
  default     = "LRS"
  description = "The replication type to use for the storage account the CPS bot's AI Search resource's datasource will connect to"
}

variable "currency_code" {
  type        = string
  default     = "USD"
  description = "Currency code for the Power Platform environment"
}

variable "enable_telemetry" {
  type        = bool
  default     = true
  description = <<DESCRIPTION
This variable controls whether or not telemetry is enabled for the module.
For more information see <https://aka.ms/avm/telemetryinfo>.
If it is set to false, then no telemetry will be collected.
DESCRIPTION
}

variable "environment_type" {
  type        = string
  default     = "Sandbox"
  description = "The type of the Power Platform environment to be deployed."
}

variable "failover_ai_search_subnet_address_spaces" {
  type        = list(string)
  default     = ["10.2.0.0/24"]
  description = "AI Search subnet address spaces. Ensure there are no collisions with existing subnets."
}

variable "failover_location" {
  type        = string
  default     = "westus"
  description = "Failover region for deployment."
}

variable "failover_subnet_address_spaces" {
  type        = list(string)
  default     = ["10.2.1.0/24"]
  description = "Failover subnet address spaces."
}

variable "failover_subnet_name" {
  type        = string
  default     = "power-platform-failover-subnet"
  description = "The name of the failover subnet. Used in the Power Platform Enterprise Policy network connection."
}

variable "failover_vnet_address_spaces" {
  type        = list(string)
  default     = ["10.2.0.0/16"]
  description = "Failover virtual network address spaces."
}

variable "include_app_insights" {
  type        = bool
  default     = false
  description = "Include Application Insights in the deployment."
}

variable "language_code" {
  type        = number
  default     = 1033 # English
  description = "Language code for the Power Platform environment"
}

variable "location" {
  type        = string
  default     = "eastus"
  description = "Region where the resources should be deployed."
  nullable    = false
}

variable "power_platform_environment" {
  type = object({
    name              = string
    id                = string # Optional. If provided, the module will attempt to use the existing environment. If left blank, a new environment will be created.
    language_code     = number
    currency_code     = string
    security_group_id = string
    environment_type  = string
    location          = string
  })
  default = {
    name              = "Copilot Studio + Azure AI"
    id                = ""                                     # Optional. If provided, the module will attempt to use the existing environment. If left blank, a new environment will be created.
    language_code     = 1033                                   # English
    security_group_id = "00000000-0000-0000-0000-000000000000" # Optional. If provided, the module will attempt to expose the environment to the specified security group.
    currency_code     = "USD"
    environment_type  = "Sandbox"
    location          = "unitedstates"
  }
  description = <<DESCRIPTION
  - `name`: The name of the Power Platform environment to be managed.
  - `language_code`: The language code for the Power Platform environment.
  - `currency_code`: The currency code for the Power Platform environment.
  - `security_group_id`: The ID of the security group to be used for initial access to the Power Platform environment.
  - `environment_type`: The type of the Power Platform environment to be managed.
  - `location`: The location of the Power Platform environment.
DESCRIPTION
}

variable "power_platform_managed_environment" {
  type = object({
    id                         = string # Optional. If provided, the module will attempt to use the existing managed environment. If left blank, a new environment will be created.
    is_usage_insights_disabled = bool
    is_group_sharing_disabled  = bool
    limit_sharing_mode         = string
    max_limit_user_sharing     = number
    solution_checker_mode      = string
    suppress_validation_emails = bool
    maker_onboarding_markdown  = string
    maker_onboarding_url       = string
  })
  default = {
    id                         = "" # Optional. If provided, the module will attempt to use the existing managed environment. If left blank, a new environment will be created.
    is_usage_insights_disabled = false
    is_group_sharing_disabled  = false
    limit_sharing_mode         = "ExcludeSharingToSecurityGroups"
    max_limit_user_sharing     = 0
    solution_checker_mode      = "None"
    suppress_validation_emails = false
    maker_onboarding_markdown  = ""
    maker_onboarding_url       = ""
  }
  description = "Configuration for the Power Platform managed environment"
}

variable "power_platform_oai_connection_display_name" {
  type        = string
  default     = "Copilot OpenAI Connection"
  description = "The display name of the connection between the Power Platform environment and the OpenAI model in Azure."
}

variable "primary_ai_search_subnet_address_spaces" {
  type        = list(string)
  default     = ["10.1.7.0/24"]
  description = "AI Search subnet address spaces. Ensure there are no collisions with existing subnets."
}

variable "primary_location" {
  type        = string
  default     = "eastus"
  description = "Primary region for deployment."
}

variable "primary_subnet_address_spaces" {
  type        = list(string)
  default     = ["10.1.6.0/24"]
  description = "Primary subnet address spaces. Ensure there are no collisions with existing subnets."
}

variable "primary_subnet_name" {
  type        = string
  default     = "power-platform-primary-subnet"
  description = "The name of the primary subnet. Used in the Power Platform Enterprise Policy network connection."
}

variable "primary_vnet_address_spaces" {
  type        = list(string)
  default     = ["10.1.0.0/16"]
  description = "Primary virtual network address spaces."
}

variable "resource_prefix" {
  default     = "cpmonitor"
  description = "Prefix for all resource names"
}

variable "resource_suffix" {
  default     = "001"
  description = "Suffix for all resource names"
}

variable "search_index_name" {
  type        = string
  default     = "default_index"
  description = "The name of the AI Search index"
}

variable "search_indexer_name" {
  type        = string
  default     = "default-cps-indexer"
  description = "The name of the indexer"
}

variable "tags" {
  type = map(string)
  default = {
    environment = "test - copilot + azure AI"
    cicd        = "terraform"
    name        = "PTN-CPS-AZAI"
  }
  description = "The location for the resources."
}
