# Basic usage of the Copilot Studio module. Assumes the usage of AVMs to initialize minimal prerequisite resources.

locals {
  search_endpoint_url = "https://${azurerm_search_service.ai_search.name}.search.windows.net"
  env_tags            = { azd-env-name : var.azd_environment_name }
}

# The unique ID that will be included in most resources managed by this module
resource "random_string" "name" {
  length  = 5
  numeric = false
  special = false
  upper   = false
}

# The Resource Group that will contain the resources managed by this module
resource "azurerm_resource_group" "this" {
  location = var.location
  name     = "rg-${random_string.name.id}"
  tags     = merge(var.tags, local.env_tags)
}

module "copilot_studio" {
  source = "./modules/copilot_studio"

  # Basic properties
  # name                = "Copilot + Azure AI"
  resource_group_name = azurerm_resource_group.this.name
  unique_id           = random_string.name.id # The unique ID to include with any resources generated by this module

  tags = merge(var.tags, local.env_tags)
  # Authentication inputs
  resource_share_user = var.resource_share_user

  # Network inputs
  primary_vnet_name    = module.primary_virtual_network.name
  primary_subnet_name  = var.primary_subnet_name
  failover_vnet_name   = module.failover_virtual_network.name
  failover_subnet_name = var.failover_subnet_name

  # Power Platform properties
  # Note: the Power Platform environment variables have default values that will 
  #  be used to generate new resources. If you have an existing environment you'd 
  #  prefer to manage instead, specify the environment IDs in the variable objects
  #  and the module will attempt to manage the existing environment instead. 
  power_platform_environment         = var.power_platform_environment
  power_platform_managed_environment = var.power_platform_managed_environment
  # Add Power Platform connections. For the MCS example we need the AOAI connection 
  # and AI Search connection, but this list could take any supported connectors.
  power_platform_connections = [
    {
      name         = "shared_azureopenai"
      display_name = "Azure OpenAI Connection"
      connection_parameters = {
        azureOpenAIResourceName = module.azure_open_ai.name
        azureOpenAIApiKey       = module.azure_open_ai.primary_access_key
        azureSearchEndpointUrl  = local.search_endpoint_url
        azureSearchApiKey       = azurerm_search_service.ai_search.primary_key
      }
      connector_name = "azureopenai"
    },
    {
      name         = "shared_azureaisearch"
      display_name = "Azure AI Search Connection"
      connection_parameters = {
        ConnectionEndpoint = local.search_endpoint_url
        AdminKey           = azurerm_search_service.ai_search.primary_key
      }
      connector_name = "azureaisearch"
    }
  ]
}

# Example tenant settings to support generative answers in Copilot.
# Given the sensitivity and different security of tenant settings,
# this resource is block commented but may be used if allowed and desired.

# resource "powerplatform_tenant_settings" "settings" {
#   power_platform = {
#     power_automate = {
#       disable_copilot = false
#       # Optional
#       disable_copilot_with_bing = false
#     }
#     intelligence = {
#       disable_copilot               = false
#       enable_open_ai_bot_publishing = true
#     }
#   }
# }
