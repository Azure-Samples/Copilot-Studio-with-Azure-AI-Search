# Basic usage of the Copilot Studio module. Assumes the usage of AVMs to initialize minimal prerequisite resources.

locals {
  search_endpoint_url = "https://${azurerm_search_service.ai_search.name}.search.windows.net"
  env_tags            = { azd-env-name : var.azd_environment_name }
}

# The unique ID that will be included in most resources managed by this module
resource "random_string" "name" {
  length  = 5
  numeric = false
  special = false
  upper   = false
}

# The Resource Group that will contain the resources managed by this module
resource "azurerm_resource_group" "this" {
  location = var.location
  name     = "rg-${random_string.name.id}"
  tags     = merge(var.tags, local.env_tags)
}

module "copilot_studio" {
  source = "./modules/copilot_studio"

  # Basic properties
  resource_group_name = azurerm_resource_group.this.name
  unique_id           = random_string.name.id # The unique ID to include with any resources generated by this module

  tags = merge(var.tags, local.env_tags)
  # Authentication inputs
  resource_share_user = var.resource_share_user
  # Network inputs
  primary_vnet_name    = azurerm_virtual_network.primary_virtual_network.name
  primary_subnet_name  = azurerm_subnet.primary_subnet.name
  failover_vnet_name   = azurerm_virtual_network.failover_virtual_network.name
  failover_subnet_name = azurerm_subnet.failover_subnet.name

  # Power Platform properties
  # Note: the Power Platform environment variables have default values that will
  #  be used to generate new resources. If you have an existing environment you'd
  #  prefer to manage instead, specify the environment IDs in the variable objects
  #  and the module will attempt to manage the existing environment instead.
  power_platform_environment         = var.power_platform_environment
  power_platform_managed_environment = var.power_platform_managed_environment

  billing_subscription_id = data.azurerm_client_config.current.subscription_id
}

# Example tenant settings to support generative answers in Copilot.
# Given the sensitivity and different security of tenant settings,
# this resource is block commented but may be used if allowed and desired.

# resource "powerplatform_tenant_settings" "settings" {
#   power_platform = {
#     power_automate = {
#       disable_copilot = false
#       # Optional
#       disable_copilot_with_bing = false
#     }
#     intelligence = {
#       disable_copilot               = false
#       enable_open_ai_bot_publishing = true
#     }
#   }
# }