# Basic usage of the Copilot Studio module. Assumes the usage of AVMs to initialize minimal prerequisite resources.

locals {
  search_endpoint_url = "https://${azurerm_search_service.ai_search.name}.search.windows.net"
  env_tags            = { azd-env-name : var.azd_environment_name }

  # Resource group logic - use existing or create new
  use_existing_resource_group = var.resource_group_name != null && length(var.resource_group_name) > 0
  resource_group_name         = local.use_existing_resource_group ? var.resource_group_name : azurerm_resource_group.this[0].name
  resource_group_location     = local.use_existing_resource_group ? data.azurerm_resource_group.existing[0].location : var.location
  
  # Organization suffiexes and prefixes are optional, and we need to form an array of non-empty values only
  org_prefix                  = compact([var.org_naming.org_prefix])
  org_suffix                  = compact([var.org_naming.org_environment, var.org_naming.org_suffix])
}


# Data source to validate existing resource group exists
data "azurerm_resource_group" "existing" {
  count = local.use_existing_resource_group ? 1 : 0
  name  = var.resource_group_name
}

# Generate unique names for primary resources
resource "azurecaf_name" "main_names" {
  name          = var.org_naming.workload_name
  resource_types = [
    "azurerm_resource_group",
    "azurerm_storage_account",
    "azurerm_search_service",
    "azurerm_cognitive_account",
    "azurerm_virtual_network",
    "azurerm_network_security_group",
    "azurerm_virtual_network_gateway",
    "azurerm_public_ip"
  ]
  prefixes      = local.org_prefix
  suffixes      = local.org_suffix
  random_length = 4
  # use_slug = false
  clean_input   = true
}

# Generate unique names for failover resources
resource "azurecaf_name" "failover_names" {
  name          = var.org_naming.workload_name
  resource_types = [
    "azurerm_virtual_network",
    "azurerm_network_security_group",
    "azurerm_virtual_network_gateway",
    "azurerm_public_ip"
  ]
  prefixes      = local.org_prefix
  suffixes      = concat(local.org_suffix, ["failover"])
  random_length = 4
  # use_slug = false
  clean_input   = true
}

# Generate unique names for primary private endpoint subnet
resource "azurecaf_name" "main_pe_subnet_names" {
  name          = var.org_naming.workload_name
  resource_types = [
    "azurerm_subnet"
  ]
  prefixes      = concat(["pe"], local.org_prefix)
  suffixes      = concat(local.org_suffix, ["primary"])
  random_length = 4
  # use_slug = false
  clean_input   = true
}

# Generate unique names for failover private endpoint subnet
resource "azurecaf_name" "failover_pe_subnet_names" {
  name          = var.org_naming.workload_name
  resource_types = [
    "azurerm_subnet"
  ]
  prefixes      = concat(["pe"], local.org_prefix)
  suffixes      = concat(local.org_suffix, ["failover"])
  random_length = 4
  # use_slug = false
  clean_input   = true
}

# Generate unique names for Azure Deployment Script related resources
resource "azurecaf_name" "deployment_script_names" {
  name          = var.org_naming.workload_name
  resource_types = [
    "azurerm_storage_account",
    "azurerm_network_security_group",
    "azurerm_subnet",
    "azurerm_user_assigned_identity"
  ]
  prefixes      = local.org_prefix
  suffixes      = concat(local.org_suffix, ["script"])
  random_length = 4
  # use_slug = false
  clean_input   = true
}

# The unique ID that will be included in most resources managed by this module
resource "random_string" "name" {
  length  = 5
  numeric = false
  special = false
  upper   = false
}

# The Resource Group that will contain the resources managed by this module (only created if not using existing)
resource "azurerm_resource_group" "this" {
  count    = local.use_existing_resource_group ? 0 : 1
  location = var.location
  name     = azurecaf_name.main_names.results["azurerm_resource_group"]
  tags     = merge(var.tags, local.env_tags)
}

module "copilot_studio" {
  source = "./modules/copilot_studio"

  # Basic properties
  resource_group_name = local.primary_virtual_network_resource_group
  unique_id           = random_string.name.id # The unique ID to include with any resources generated by this module

  tags = merge(var.tags, local.env_tags)
  # Authentication inputs
  resource_share_user = var.resource_share_user
  # Network inputs
  primary_vnet_name    = local.primary_virtual_network_name
  primary_subnet_name  = local.primary_subnet_name
  failover_vnet_name   = local.failover_virtual_network_name
  failover_subnet_name = local.failover_subnet_name

  # Power Platform properties
  # Note: the Power Platform environment variables have default values that will
  #  be used to generate new resources. If you have an existing environment you'd
  #  prefer to manage instead, specify the environment IDs in the variable objects
  #  and the module will attempt to manage the existing environment instead.
  power_platform_environment         = var.power_platform_environment
  power_platform_managed_environment = var.power_platform_managed_environment

  # Power Platform billing policy configuration
  # Controls whether the module should create a new billing policy or use an existing one
  power_platform_billing_policy = {
    name          = var.power_platform_billing_policy.name
    should_create = var.use_billing_policy
  }
  subscription_id = data.azurerm_client_config.current.subscription_id
}

# Example tenant settings to support generative answers in Copilot.
# Given the sensitivity and different security of tenant settings,
# this resource is block commented but may be used if allowed and desired.

# resource "powerplatform_tenant_settings" "settings" {
#   power_platform = {
#     power_automate = {
#       disable_copilot = false
#       # Optional
#       disable_copilot_with_bing = false
#     }
#     intelligence = {
#       disable_copilot               = false
#       enable_open_ai_bot_publishing = true
#     }
#   }
# }